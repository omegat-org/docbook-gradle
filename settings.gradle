pluginManagement {
    repositories {
        mavenCentral()
        gradlePluginPortal()
        // 'repository' for getting docbook xsd
        ivy {
            url = 'https://docbook.org/xml'
            patternLayout {
                artifact '[revision]/[module]-[revision].[ext]'
            }
            content {
                includeGroup 'docbook'
            }
            metadataSources {
                artifact()
            }
        }
    }
    // Include 'plugins build' to define convention plugins.
    includeBuild("build-logic")
}

plugins {
    id 'org.gradle.toolchains.foojay-resolver-convention' version '0.9.0'
    id 'com.gradle.develocity' version '3.19.1'
}

develocity {
    buildScan {
        publishing.onlyIf { "true".equals(System.getProperty("envIsCi")) }
        termsOfUseUrl = "https://gradle.com/terms-of-service"
        termsOfUseAgree = "yes"
    }
}
rootProject.name = 'omegat-manual'
